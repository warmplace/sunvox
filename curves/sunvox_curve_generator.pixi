/*
  SUNVOX CURVE FILE GENERATOR
  How to use:
    1) change the SETUP section:
       filename = "file name for your curve";
       format = 
         0 - MultiSynth curve1;
         1 - MultiSynth curve2;
         2 - MultiSynth curve3 (pitch);
         3 - WaveShaper;
         4 - MultiCtl.
       curve_function - here you can place you own function for the specified curve;
    2) run this program using the Pixilang - https://warmplace.ru/soft/pixilang
  License: public domain.
*/

// ########################################
// ## SETUP ###############################
// ########################################

//Filename:
filename = "sunvox_curve"

//Format:
format = 2
if format == 0
{
    //MultiSynth curve1: X = note; Y = velocity (0..255)
    type = INT8
    items = 128
    format_name = "MultiSynth Curve 1"
    mod_name = "MultiSynth"
}
if format == 1
{
    //MultiSynth curve2: X = velocity; Y = velocity (0..255)
    type = INT8
    items = 257
    format_name = "MultiSynth Curve 2"
    mod_name = "MultiSynth"
}
if format == 2
{
    //MultiSynth curve3: X = note; Y = pitch (0..65535);
    //pitch range: 0 ... 16384 (note0) ... 49152 (note=128) ... 65535 (note=191.996); semitone = 256;
    note0 = 16384
    semitone = 256
    x_not_normalized = 1
    type = INT16
    items = 128
    format_name = "MultiSynth Curve 3"
    mod_name = "MultiSynth"
}
if format == 3
{
    //WaveShaper: X = input; Y = output (0..65535)
    type = INT16
    items = 256
    format_name = "WaveShaper"
    mod_name = "WaveShaper"
}
if format == 4
{
    //MultiCtl: X = input; Y = output (0..32768)
    type = INT16
    items = 257
    max = 32768
    format_name = "MultiCtl"
    mod_name = "MultiCtl"
}

//Function:
//$x = 0...1.0  or  0,1,2,3...127 (note) for MultiSynth Pitch Curve3
//$y = 0...1.0  or  0...65535 (pitch) for MultiSynth Pitch Curve3
fn curve_function( $x ) 
{
    //Linear:
    $y = $x

    //Reverse:
    //$y = 1 - $x

    //Linear with a limited number of levels:
    //$levels = 8 ; $y = ( $x / (1/$levels) div 1 ) * (1/($levels-1))

    //Sine variations:
    //$y = sin( $x * M_PI * 2 ) / 2 + 0.5
    //$y = sin( $x * M_PI )
    //$y = -sin( $x * M_PI + M_PI/2 ) / 2 + 0.5

    //Random:
    //$y = rand() / 32768


    //Exponential curves:

    //$y = pow( $x, 3 )
    //$y = pow( $x, 6 ) //Exponential+
    //$y = pow( $x, 0.5 ) //...

    //Something between the linear and the exponential curve:
    //$y = $x * $x + pow( $x, 3 ) * ( 1 - $x )



    //Pitch Curves:

    //Normal scale:
    //$y = note0 + semitone * $x

    //Reverse scale:
    //$y = note0 + semitone * ( 128 - $x )

    //Diatonic scale (major):
    //$y = note0 + semitone * remap_note( $x, "CCDDEFFGGAAB" )

    //Harmonic major scale:
    //$y = note0 + semitone * remap_note( $x, "CCDDEFFGgggB" )

    //Melodic major scale:
    //$y = note0 + semitone * remap_note( $x, "CCDDEFFGggaa" )

    //Double harmonic major scale:
    //$y = note0 + semitone * remap_note( $x, "CcccEFFGgggB" )

    //Minor scale:
    //$y = note0 + semitone * remap_note( $x, "CCDddFFGggaa" )

    //Whole tone scale (six-note)
    //$y = note0 + semitone * ( $x div 2 * 2 )

    //Minor pentatonic:
    //$y = note0 + semitone * remap_note( $x, "CCddFFFGGaaa" )

    //Major pentatonic:
    //$y = note0 + semitone * remap_note( $x, "CCDDEEEGGAAA" )

    //Custom tuning (you can set the offset in cents):
    /*
    $y = note0 + semitone * tuning_cents_offset( $x,
        //example: Standard Just (1529)
        15.64, //C
        -13.69, //c
        -1.95, //D
        31.28, //d
        1.96, //E
        13.69, //F
	-15.64, //f
        17.6, //G
        -11.73, //g
        0, //A
        11.73, //a
        3.91 //B
    )
    */

    //Tuning from the file with a list of frequencies (in Hz, frequency per line):
    //if freq_list <= 0 { freq_list = load_list( "freq_series.txt" ) } ; $y = freq_to_pitch( freq_list[ $x ] )

    //Equal temperament (ET):
    /*
    $tones_per_octave = 96;
    $note_shift = 0 //the note coinciding with C0 (note 0) in the default 12-TET scale
    $pitch_shift = semitone * 12 * 4;
    $y = note0 + $pitch_shift + semitone * 12 / $tones_per_octave * ( $x - $note_shift )
    */

    ret( $y )
}

// ########################################
// ## SETUP COMPLETE ######################
// ########################################

set_pixel_size( WINDOW_XSIZE / 480 )
resize( get_screen(), WINDOW_XSIZE, WINDOW_YSIZE )
scr = get_screen()
xsize = get_xsize( scr )
ysize = get_ysize( scr )

clear( BLACK )
curve = new( items, 1, type )
clean( curve )
if max == 0 
{
    if type == INT8 { max = 255 }
    if type == INT16 { max = 65535 }
}
i = 0 while i < items
{
    if x_not_normalized
    {
	v = curve_function( i )
	if v < 0 { v = 0 }
	if v > max { v = max }
    }
    else
    {
	v = curve_function( i / ( items - 1 ) )
	if v < 0 { v = 0 }
	if v > 1 { v = 1 }
	if type == INT8 { v * max }
	if type == INT16 { v * max }
    }
    logf( "%d: %d\n", i, v )
    curve[ i ] = v
    x = i / items * xsize - xsize / 2
    y = ( -v / max + 0.5 ) * ysize
    if i > 0
    {
	fbox( prev_x + xsize/items/2, y, x - prev_x + 1, ysize * 2, #404040 )
	fbox( prev_x + xsize/items/2, y, x - prev_x + 1, 1, #808080 )
    }
    prev_x = x
    i + 1
}
save( curve, filename, FORMAT_RAW )

ts=""
sprintf( ts, "File %s just generated.\nNow you can load it in SunVox\n(%s -> LOAD)", filename, mod_name )
print( ts )

while 1
{
    frame( 100 )
    while get_event() { if EVT[ EVT_TYPE ] == EVT_QUIT { halt } }
}

fn remap_note( $n, $notes )
{
    $len = strlen( $notes )
    $nn = $n % $len
    $oct = $n div $len
    $c = $notes[ $nn ]
    while 1
    {
	if $c == 'C' { $c = 0 break }
	if $c == 'c' { $c = 1 break }
	if $c == 'D' { $c = 2 break }
	if $c == 'd' { $c = 3 break }
	if $c == 'E' { $c = 4 break }
	if $c == 'F' { $c = 5 break }
	if $c == 'f' { $c = 6 break }
	if $c == 'G' { $c = 7 break }
	if $c == 'g' { $c = 8 break }
	if $c == 'A' { $c = 9 break }
	if $c == 'a' { $c = 10 break }
	if $c == 'B' { $c = 11 break }
	break
    }
    ret( $oct * $len + $c )
}
//normal scale: CcDdEFfGgAaB

fn tuning_cents_offset( $n, $zero,$one,$two,$three,$four,$five,$six,$seven,$eight,$nign,$ten,$eleven )
{
    $oct_len = 12
    $nn = $n % $oct_len
    $oct = $n div $oct_len
    while 1
    {
        if $nn == 0 { $add = $2 break }
        if $nn == 1 { $add = $3 break }
        if $nn == 2 { $add = $4 break }
        if $nn == 3 { $add = $5 break }
        if $nn == 4 { $add = $6 break }
        if $nn == 5 { $add = $7 break }
        if $nn == 6 { $add = $8 break }
        if $nn == 7 { $add = $9 break }
        if $nn == 8 { $add = $10 break }
        if $nn == 9 { $add = $11 break }
        if $nn == 10 { $add = $12 break }
        if $nn == 11 { $add = $13 break }
        break
    }
    ret( $oct * $oct_len + $nn + $add/100 )
}

fn load_list( $fname )
{
    $f = fopen( $fname, "rb" )
    if $f == 0 { ret( -1 ) }
    $ts = new( 256, 1, INT8 )
    $list = new( 1024, 1, FLOAT )
    $list_size = 0
    while 1
    {
	$len = fgets( $ts, 256, $f )
	if $len <= 0 { break }
	$list[ $list_size ] = str2num( $ts )
	$list_size + 1
    }
    if $list_size == 0 { remove( $list ) $list = -1 }
    resize( $list, $list_size, 1, FLOAT )
    remove( $ts )
    fclose( $f )
    ret( $list )
}

fn freq_to_pitch( $freq ) //from frequency (in Hz) to pitch (MultiSynth Curve3):
{
    ret( log2( $freq / 16.333984375 ) * 3072 + 16384 )
}
